/*
 * Nextcloud Talk - Android Client
 *
 * SPDX-FileCopyrightText: 2017-2024 Nextcloud GmbH and Nextcloud contributors
 * SPDX-FileCopyrightText: 2021-2023 Marcel Hibbe <dev@mhibbe.de>
 * SPDX-FileCopyrightText: 2022 Tim Kr√ºger <t@timkrueger.me>
 * SPDX-FileCopyrightText: 2021 Andy Scherzinger <info@andy-scherzinger.de>
 * SPDX-FileCopyrightText: 2017-2019 Mario Danic <mario@lovelyhq.com>
 * SPDX-License-Identifier: GPL-3.0-or-later
 */

plugins {
    id "org.jetbrains.kotlin.plugin.compose" version "2.2.20"
    id "org.jetbrains.kotlin.kapt"
    id 'com.google.devtools.ksp' version '2.2.20-2.0.3'
    id "de.undercouch.download" version "5.6.0" // Download task and extension for retrieving files during a build
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 35

    namespace = 'com.nextcloud.talk'

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 35
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        // mayor.minor.hotfix.increment (for increment: 01-50=Alpha / 51-89=RC / 90-99=stable)
        // xx   .xxx  .xx    .xx
        versionCode 230000011
        versionName "23.0.0 Alpha 11"

        flavorDimensions "default"
        renderscriptTargetApi = 19
        renderscriptSupportModeEnabled true

        productFlavors {
            // used for f-droid
            generic {
                applicationId 'com.nextcloud.talk2'
                dimension "default"
            }
            gplay {
                applicationId 'com.nextcloud.talk2'
                dimension "default"
            }
            qa {
                applicationId "com.nextcloud.talk2.qa"
                dimension "default"
                versionCode 1
                versionName "1"
            }
        }

        // Enabling multidex support.
        multiDexEnabled = true

        vectorDrawables.useSupportLibrary = true

        lintOptions {
            disable 'InvalidPackage'
            disable 'MissingTranslation'
            disable 'VectorPath'
            disable 'UnusedQuantity'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        sourceSets {
            // Adds exported schema location as test app assets.
            getByName("androidTest").assets.srcDir("$projectDir/schemas")
        }

        testInstrumentationRunnerArgument "TEST_SERVER_URL", "${NC_TEST_SERVER_BASEURL}"
        testInstrumentationRunnerArgument "TEST_SERVER_USERNAME", "${NC_TEST_SERVER_USERNAME}"
        testInstrumentationRunnerArgument "TEST_SERVER_PASSWORD", "${NC_TEST_SERVER_PASSWORD}"

        def localBroadcastPermission = "PRIVATE_BROADCAST"
        manifestPlaceholders.broadcastPermission = localBroadcastPermission
        buildConfigField "String", "PERMISSION_LOCAL_BROADCAST", "\"${localBroadcastPermission}\""
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
        unitTests.returnDefaultValues = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        resources {
            excludes += [
                'META-INF/LICENSE.txt',
                'META-INF/LICENSE',
                'META-INF/NOTICE.txt',
                'META-INF/NOTICE',
                'META-INF/DEPENDENCIES',
                'META-INF/rxjava.properties'
            ]
        }
    }

    check.dependsOn 'lint', 'ktlintCheck'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
        compose = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.15"
    }

    lint {
        abortOnError = false
        disable 'MissingTranslation','PrivateResource'
        htmlOutput = layout.buildDirectory.file("reports/lint/lint.html").get().asFile
        htmlReport = true
    }
}
kapt {
    correctErrorTypes = true
}

ext {
    androidxCameraVersion = "1.5.0"
    coilKtVersion = "2.7.0"
    daggerVersion = "2.57.1"
    emojiVersion = "1.6.0"
    fidoVersion = "4.1.0-patch2"
    lifecycleVersion = '2.9.4'
    okhttpVersion = "4.12.0"
    markwonVersion = "4.6.2"
    materialDialogsVersion = "3.3.0"
    parcelerVersion = "1.1.13"
    prismVersion = "2.0.0"
    retrofit2Version = "3.0.0"
    roomVersion = "2.8.0"
    workVersion = "2.10.4"
    espressoVersion = "3.7.0"
    androidxTestVersion = "1.5.0"
    media3_version = "1.8.0"
    coroutines_version = "1.10.2"
    mockitoKotlinVersion = "6.0.0"
}

configurations.configureEach {
    exclude group: 'com.google.firebase', module: 'firebase-core'
    exclude group: 'com.google.firebase', module: 'firebase-analytics'
    exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    exclude group: 'org.jetbrains', module: 'annotations-java5' // via prism4j, already using annotations explicitly
}

project.ext.ASSET_DIR = projectDir.toString() + '/src/main/assets'
apply from: 'download_model.gradle'

dependencies {
    implementation "androidx.room:room-testing-android:${roomVersion}"
    implementation 'androidx.compose.foundation:foundation-layout:1.9.2'

    implementation("androidx.compose.runtime:runtime:1.9.1")
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.datastore:datastore-core:1.1.7'
    implementation 'androidx.datastore:datastore-preferences:1.1.7'
    implementation 'androidx.test.ext:junit-ktx:1.3.0'
	
    implementation fileTree(include: ['*'], dir: 'libs')

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0"

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation "com.vanniktech:emoji-google:0.21.0"
    implementation "androidx.emoji2:emoji2:${emojiVersion}"
    implementation "androidx.emoji2:emoji2-bundled:${emojiVersion}"
    implementation "androidx.emoji2:emoji2-views:${emojiVersion}"
    implementation "androidx.emoji2:emoji2-views-helper:${emojiVersion}"
    implementation 'org.michaelevans.colorart:library:0.0.3'
    implementation "androidx.work:work-runtime:${workVersion}"
    implementation "androidx.work:work-rxjava2:${workVersion}"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation ('com.github.bitfireAT:dav4jvm:2.1.3', {
        exclude group: 'org.ogce', module: 'xpp3'	// Android comes with its own XmlPullParser
    })
    implementation 'org.conscrypt:conscrypt-android:2.5.3'
    implementation "com.github.nextcloud-deps:qrcodescanner:0.1.2.4" // "com.github.blikoon:QRCodeScanner:0.1.2"

    implementation "androidx.camera:camera-core:${androidxCameraVersion}"
    implementation "androidx.camera:camera-camera2:${androidxCameraVersion}"
    implementation "androidx.camera:camera-lifecycle:${androidxCameraVersion}"
    implementation "androidx.camera:camera-view:${androidxCameraVersion}"
    implementation "androidx.exifinterface:exifinterface:1.4.1"

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:${lifecycleVersion}"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${lifecycleVersion}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${lifecycleVersion}"
    implementation "androidx.lifecycle:lifecycle-process:${lifecycleVersion}"
    implementation "androidx.lifecycle:lifecycle-common:${lifecycleVersion}"
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:${lifecycleVersion}")

    implementation 'androidx.biometric:biometric:1.1.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "io.reactivex.rxjava2:rxjava:2.2.21"

    implementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:${okhttpVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"

    implementation 'com.bluelinelabs:logansquare:1.3.7'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.20.0'
    kapt 'com.bluelinelabs:logansquare-compiler:1.3.7'

    implementation "com.squareup.retrofit2:retrofit:${retrofit2Version}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofit2Version}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofit2Version}"
    implementation 'de.mannodermaus.retrofit2:converter-logansquare:1.4.1'

    implementation "com.google.dagger:dagger:${daggerVersion}"
    kapt "com.google.dagger:dagger-compiler:${daggerVersion}"
    implementation 'com.github.lukaspili.autodagger2:autodagger2:1.1'
    kapt 'com.github.lukaspili.autodagger2:autodagger2-compiler:1.1'
    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'net.zetetic:sqlcipher-android:4.10.0'

    implementation "androidx.room:room-runtime:${roomVersion}"
    implementation "androidx.room:room-rxjava2:${roomVersion}"
    ksp "androidx.room:room-compiler:${roomVersion}"
    implementation "androidx.room:room-ktx:${roomVersion}"

    implementation "org.parceler:parceler-api:$parcelerVersion"
    implementation 'com.github.ddB0515.FlexibleAdapter:flexible-adapter:5.1.1'
    implementation 'com.github.ddB0515.FlexibleAdapter:flexible-adapter-ui:5.1.1'
    implementation 'org.apache.commons:commons-lang3:3.19.0'
    implementation 'com.github.wooplr:Spotlight:1.3'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.github.nextcloud-deps:ChatKit:0.4.2'
    implementation 'joda-time:joda-time:2.14.0'
    implementation "io.coil-kt:coil:${coilKtVersion}"
    implementation "io.coil-kt:coil-gif:${coilKtVersion}"
    implementation "io.coil-kt:coil-svg:${coilKtVersion}"
    implementation "io.coil-kt:coil-compose:${coilKtVersion}"
    implementation 'com.github.natario1:Autocomplete:1.1.0'

    implementation "com.github.nextcloud-deps.hwsecurity:hwsecurity-fido:${fidoVersion}"
    implementation "com.github.nextcloud-deps.hwsecurity:hwsecurity-fido2:${fidoVersion}"

    implementation "com.afollestad.material-dialogs:core:${materialDialogsVersion}"
    implementation "com.afollestad.material-dialogs:datetime:${materialDialogsVersion}"
    implementation "com.afollestad.material-dialogs:bottomsheets:${materialDialogsVersion}"
    implementation "com.afollestad.material-dialogs:lifecycle:${materialDialogsVersion}"

    implementation 'com.google.code.gson:gson:2.13.2'

    implementation "androidx.media3:media3-exoplayer:$media3_version"
    implementation "androidx.media3:media3-ui:$media3_version"

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.29'

    implementation "io.noties.markwon:core:$markwonVersion"
    implementation "io.noties.markwon:ext-strikethrough:$markwonVersion"
    implementation "io.noties.markwon:ext-tasklist:$markwonVersion"
    implementation "io.noties.markwon:ext-tables:$markwonVersion"

    implementation 'com.github.nextcloud-deps:ImagePicker:2.1.0.2'
    implementation 'io.github.elye:loaderviewlibrary:3.0.0'
    implementation 'org.osmdroid:osmdroid-android:6.1.20'
    implementation ('fr.dudie:nominatim-api:3.4', {
        //noinspection DuplicatePlatformClasses
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    })

    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.activity:activity-ktx:1.10.1'
    implementation 'com.github.nextcloud.android-common:ui:0.28.0'
    implementation 'com.github.nextcloud-deps:android-talk-webrtc:132.6834.0'

    gplayImplementation 'com.google.android.gms:play-services-base:18.8.0'
    gplayImplementation "com.google.firebase:firebase-messaging:25.0.1"

    //compose
    implementation(platform("androidx.compose:compose-bom:2025.09.00"))
    implementation("androidx.compose.ui:ui")
    implementation 'androidx.compose.material3:material3:1.3.2'
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation 'androidx.activity:activity-compose:1.10.1'
    debugImplementation("androidx.compose.ui:ui-tooling")

    //tests
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.0'
    androidTestImplementation("androidx.compose.ui:ui-test-junit4:1.9.2")
    debugImplementation("androidx.compose.ui:ui-test-manifest")

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.20.0'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'

    androidTestImplementation "androidx.test:core:1.7.0"

    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2"
    androidTestImplementation 'androidx.test:core-ktx:1.7.0'
    androidTestImplementation 'org.mockito:mockito-android:5.20.0'
    androidTestImplementation "androidx.work:work-testing:${workVersion}"
    // Espresso core
    androidTestImplementation ("androidx.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-web:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-accessibility:$espressoVersion"

    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"

    androidTestImplementation('com.android.support.test.espresso:espresso-intents:3.0.2')

    androidTestImplementation(platform("androidx.compose:compose-bom:2025.09.00"))
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    testImplementation 'org.junit.vintage:junit-vintage-engine:6.0.0' // DO NOT REMOVE
    testImplementation "androidx.room:room-testing:${roomVersion}"
    testImplementation("com.squareup.okhttp3:mockwebserver:$okhttpVersion")
    testImplementation("com.google.dagger:hilt-android-testing:2.57.1")
    testImplementation("org.robolectric:robolectric:4.16")

    // Computer Vision - for background effects during video calls
    implementation 'com.google.mediapipe:tasks-vision:0.10.26'
    implementation 'org.opencv:opencv:4.12.0'
}

tasks.register('installGitHooks', Copy) {
    description = "Install git hooks"
    from("../scripts/hooks") {
        include '*'
    }
    into '../.git/hooks'
}





ksp {
    arg('room.schemaLocation', "$projectDir/schemas")
}