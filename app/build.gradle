apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'findbugs'

def taskRequest = getGradle().getStartParameter().getTaskRequests().toString()
if (taskRequest.contains("Gplay") || taskRequest.contains("findbugs") || taskRequest.contains("lint")) {
    apply from: 'gplay.gradle'
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.nextcloud.talk2"
        versionName version
        minSdkVersion 21
        targetSdkVersion 28
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        versionCode 91
        versionName "6.0.0beta3"

        flavorDimensions "default"
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true

        productFlavors {
            // used for f-droid
            generic
            gplay

        }

        // Enabling multidex support.
        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true

        lintOptions {
            disable 'InvalidPackage'
            disable 'MissingTranslation'
            disable "ValidController"
            disable "ValidControllerChangeHandler"
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        htmlReport true
        htmlOutput file("$project.buildDir/reports/lint/lint.html")
        disable 'MissingTranslation'
    }

    task findbugs(type: FindBugs) {
        ignoreFailures = false
        effort = "max"
        reportLevel = "medium"
        classes = fileTree("$project.buildDir/intermediates/classes/gplay/debug/com/nextcloud")
        excludeFilter = file("${project.rootDir}/findbugs-filter.xml")
        source = fileTree('src/main/java')
        pluginClasspath = project.configurations.findbugsPlugins
        classpath = files()
        include '**/*.java'
        exclude '**/gen/**'

        reports {
            xml.enabled = false
            html.enabled = true
            html {
                destination = file("$project.buildDir/reports/findbugs/findbugs.html")
            }
        }
    }
}

ext {
    workVersion = "1.0.0"
}


configurations.all {
    exclude group: 'com.google.firebase', module: 'firebase-core'
    exclude group: 'com.google.firebase', module: 'firebase-analytics'
    exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha4'
    implementation 'com.github.vanniktech:Emoji:746caa4623'
    implementation 'org.michaelevans.colorart:library:0.0.3'
    implementation "android.arch.work:work-runtime:${workVersion}"
    implementation "android.arch.work:work-rxjava2:${workVersion}"
    androidTestImplementation "android.arch.work:work-testing:${workVersion}"

    implementation 'androidx.biometric:biometric:1.0.0-alpha04'
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "io.reactivex.rxjava2:rxjava:2.2.7"

    implementation 'com.bluelinelabs:conductor:2.1.5'
    implementation 'com.bluelinelabs:conductor-support:2.1.5'

    implementation 'com.squareup.okhttp3:okhttp:3.13.1'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.13.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.13.1'

    implementation 'com.bluelinelabs:logansquare:1.3.7'
    annotationProcessor 'com.bluelinelabs:logansquare-compiler:1.3.7'

    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'com.github.aurae.retrofit2:converter-logansquare:1.4.1'

    implementation 'com.google.dagger:dagger:2.21'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.21'
    implementation 'com.github.lukaspili.autodagger2:autodagger2:1.1'
    annotationProcessor 'com.github.lukaspili.autodagger2:autodagger2-compiler:1.1'

    compileOnly 'javax.annotation:jsr250-api:1.0' // Android only

    implementation 'org.greenrobot:eventbus:3.1.1'

    implementation 'io.requery:requery:1.5.1'
    implementation 'io.requery:requery-android:1.5.1'
    implementation 'net.zetetic:android-database-sqlcipher:3.5.9'
    annotationProcessor 'io.requery:requery-processor:1.5.1'

    implementation 'org.parceler:parceler-api:1.1.12'
    annotationProcessor 'org.parceler:parceler:1.1.12'

    implementation 'net.orange-box.storebox:storebox-lib:1.4.0'

    compileOnly "org.projectlombok:lombok:1.18.6"
    annotationProcessor "org.projectlombok:lombok:1.18.6"

    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'

    implementation 'com.github.HITGIF:TextFieldBoxes:1.4.3'

    implementation 'eu.davidea:flexible-adapter:5.1.0'
    implementation 'eu.davidea:flexible-adapter-ui:1.0.0'

    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.9.0@aar'
    implementation 'com.facebook.fresco:fresco:1.13.0'
    implementation 'com.facebook.fresco:animated-webp:1.13.0'
    implementation 'com.facebook.fresco:webpsupport:1.13.0'
    implementation 'com.facebook.fresco:animated-gif:1.13.0'
    implementation "com.facebook.fresco:imagepipeline-okhttp3:1.13.0"
    implementation 'org.webrtc:google-webrtc:1.0.23295'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"

    implementation 'com.yarolegovich:lovely-dialog:1.1.0'
    implementation 'com.yarolegovich:lovelyinput:1.0.9'
    implementation 'com.yarolegovich:mp:1.0.9'

    implementation 'me.zhanghai.android.effortlesspermissions:library:1.1.0'

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'

    implementation 'com.github.wooplr:Spotlight:1.3'

    implementation 'com.github.mario:chatkit:d63d61db95'

    implementation 'com.github.natario1:Autocomplete:v1.1.0'

    implementation 'com.github.Kennyc1012:BottomSheet:2.4.1'
    implementation 'eu.davidea:flipview:1.2.0'
    implementation 'com.github.mario:PopupBubble:a365177d96'

    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    implementation 'com.kevalpatel2106:emoticongifkeyboard:1.1'

    implementation group: 'eu.medsea.mimeutil', name: 'mime-util', version: '2.1.3'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.26.0'
    testImplementation 'org.powermock:powermock-core:2.0.0'
    testImplementation 'org.powermock:powermock-module-junit4:2.0.0'
    testImplementation 'org.powermock:powermock-api-mockito2:2.0.0'

    androidTestImplementation ('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    findbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.8.0'
    findbugsPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:7.4.3'
}
